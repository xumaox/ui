apply plugin: 'maven'

uploadArchives {
    repositories {
        mavenDeployer {
//            add repository
            pom.version = VERSION_NAME
            pom.artifactId = POM_ARTIFACT_ID
            pom.groupId = GROUP
        }
    }
}

task installLocal(type: Upload) {
    description "Installs the artifacts to the local Maven repository."
    configuration = configurations['archives']
    repositories {
        mavenDeployer {
            pom.version = VERSION_NAME
            pom.artifactId = POM_ARTIFACT_ID
            pom.groupId = GROUP
            repository url: "file://${System.properties['user.home']}/.m2/repository"
        }
    }
}

task androidJavadocs(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))

    if (JavaVersion.current().isJava8Compatible()) {
        allprojects {
            tasks.withType(Javadoc) {
                options.addStringOption('Xdoclint:none', '-quiet')
            }
        }
    }
}

task androidJavadocsJar(type: Jar, dependsOn: androidJavadocs) {
    classifier = 'javadoc'
    from androidJavadocs.destinationDir
}

task androidSourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.sourceFiles
}

tasks.withType(Javadoc) {
    options.addStringOption('Xdoclint:none', '-quiet')
    options.addStringOption('encoding', 'UTF-8')
    options.addStringOption('charSet', 'UTF-8')
}
artifacts {
    archives androidSourcesJar
    archives androidJavadocsJar
}